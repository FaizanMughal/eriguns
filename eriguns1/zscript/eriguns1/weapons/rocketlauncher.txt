/*
 * eriguns1: rawggit lawnchair
 * (c) xaser 2017
 * sprites (c) Amuscaria
 */

class EriRocketLauncher : EriWeapon replaces RocketLauncher
{
	Default
	{
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "RocketAmmo";
		Inventory.PickupMessage "$GOTLAUNCHER";
		Tag "$TAG_ROCKETLAUNCHER";
		+WEAPON.NOAUTOFIRE;
	}
	States
	{
	Ready:
		DRLG A 1 A_WeaponReady();
		Loop;
	Deselect:
		DRLG A 1 A_Lower();
		Loop;
	Select:
		DRLG A 1 A_Raise();
		Loop;
	Fire:
		DRLG A 2 A_GunFlash();
		DRLF AB 3 Bright;
		DRLF C 2 Bright A_FireMissile();
		DRLF DE 2;
		DRLG B 1;
		DRLG C 3;
		DRLG A 2;
		DRLG A 0 A_ReFire();
		Goto Ready;
	Flash:
		TNT1 A 2 Bright A_Light(1);
		TNT1 A 2 Bright A_Light(2);
		TNT1 A 2 Bright A_Light(3);
		TNT1 A 2 Bright A_Light(2);
		TNT1 A 2 Bright A_Light(1);
		Goto LightDone;
	Spawn:
		DERL A -1;
		Stop;
	}
}

class EriRocket : Actor replaces Rocket
{
	bool noisy;

	Default
	{
		Radius 11;
		Height 8;
		Speed 20;
		Damage 20;
		Projectile;
		Scale 0.8;
		SeeSound "weapons/rocklf";
		DeathSound "weapons/rocklx";
		Obituary "$OB_MPROCKET";
		Decal "Scorch";
	}
	States
	{
	Spawn:
		RCKT A 1 Bright NoDelay X_RocketTrail();
		Loop;
	Death:
		RCKT B 4 Bright X_RocketExplode();
		RCKT CD 3 Bright;
		RCKT EF 2 Bright A_FadeOut(0.25);
		Stop;
	}
	
	void X_RocketTrail()
	{
		A_SpawnItemEx("MissileTrail", 0, 0, 0, -5, 0, 0, 0, 0);
		if(!noisy) {
			A_PlaySound("weapons/missilefly", CHAN_BODY, 0.35, 1, ATTN_NORM);
			noisy = true;
		}
	}
	
	void X_RocketExplode()
	{
		A_SetTranslucent(1.0,1);
	    A_StopSound(CHAN_BODY);
		A_Explode();
	}
}
