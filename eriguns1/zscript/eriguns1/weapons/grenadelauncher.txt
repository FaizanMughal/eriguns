// --------------------------------------------------------------------------
//
// Grenade launcher
//
// --------------------------------------------------------------------------

class EriGrenadeLauncher : EriWeapon
{
	Default
	{
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 2;
		Weapon.AmmoType "GrenadeAmmo";
		Inventory.PickupMessage "$GOTGRENADE";
		Tag "$TAG_GRENADE";
		VisibleToPlayerClass "AltPlayer";
		Inventory.RestrictedTo "AltPlayer";
		+INVENTORY.RESTRICTABSOLUTELY;
		+WEAPON.NOAUTOFIRE;
	}
	States
	{
	Ready:
		DGLG A 1 A_WeaponReady();
		Loop;
	Deselect:
		DGLG A 1 A_Lower();
		Loop;
	Select:
		DGLG A 1 A_Raise();
		Loop;
	Fire:
		DGLF A 2 A_GunFlash();
		DGLF B 2 Bright X_FireGrenade();
		DGLF C 2 Bright;
		DGLF DE 2;
		DGLG C 4;
		DGLG B 2;
		DGLG A 12;
		DGLG A 0 A_ReFire();
		Goto Ready;
	Flash:
		TNT1 A 2 Bright A_Light(2);
		TNT1 A 4 Bright A_Light(1);
		Goto LightDone;
	Spawn:
		DEGL A -1;
		Stop;
	}

	action void X_FireGrenade()
	{
		A_PlaySound("weapons/grenadefire", CHAN_WEAPON);
		A_FireProjectile("EriGrenade", 0, 1, 0, 0, 2, -5);
	}
}

class EriGrenade : Actor
{
	Default
	{
		Radius 11;
		Height 8;
		Speed 30;
		Damage 50;
		Projectile;
		Gravity 0.5;
		Scale 0.8;
		DeathSound "weapons/grenadeexplode";
		Obituary "$OB_MPGRENADE";
		Decal "Scorch";
		-NOGRAVITY;
	}
	States
	{
	Spawn:
		GRNP A 1 Bright X_GrenadeTrail();
		Wait;
	Death:
		RCKT B 4 Bright X_GrenadeExplode();
		RCKT CD 3 Bright;
		RCKT EF 2 Bright A_FadeOut(0.25);
		Stop;
	}

	void X_GrenadeTrail()
	{
		A_SpawnItemEx("EriGrenadeTrail", 0, 0, 0, -2, 0, 0, 0, 0);
	}

	void X_GrenadeExplode()
	{
		A_NoGravity();
		A_SetScale(1.2, 0.8);
		A_SetTranslucent(1.0,1);
		A_Explode(128, 256, 1, 1, 128);
	}
}

class EriGrenadeTrail : MissileTrail
{
	Default
	{
		Scale 0.25;
	}
	States
	{
	Spawn:
		MTRL HDEFG 2 Bright;
		Stop;
	}
}
