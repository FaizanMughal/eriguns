// --------------------------------------------------------------------------
//
// Auto Shotgun
//
// --------------------------------------------------------------------------

class EriAutoShotgun : EriWeapon
{
	Default
	{
		Weapon.SelectionOrder 150;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";
		EriWeapon.MagSize 12;
		Decal "BulletChip";
		Inventory.PickupMessage "$GOTAUTOSHOTGUN";
		Obituary "$OB_MPAUTOSHOTGUN";
		Tag "$TAG_AUTOSHOTGUN";
	}
	States
	{
	Ready:
		DASG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		DASG A 1 A_Lower();
		Loop;
	Select:
		DASG A 1 A_Raise();
		Loop;
	Fire:
		DASF A 2 Bright X_FireAutoShotgun();
		DASF B 2 Bright;
		DASF C 2;
		DASG A 4;
	Refire:
		DASG A 4;
		DASG A 10 A_Refire();
		Goto Ready;
	DoReload:
		DASG B 2 A_JumpIfNoAmmo("Ready");
		DASG C 2 A_PlaySound("weapons/autoshotclipout", CHAN_AUTO);
		DASG D 2;
		DASG EF 2;
		DASG G 12;
		DASG HI 2;
		DASG J 6 A_PlaySound("weapons/autoshotclipin", CHAN_AUTO);
		DASG KL 3;
		Goto Refire;
	Spawn:
		DEAS A -1;
		Stop;
	Flash:
		TNT1 A 2 Bright A_Light(1);
		TNT1 B 2 Bright A_Light(2);
		Goto LightDone;
	}

	action void X_FireAutoShotgun()
	{
		A_FireBullets(invoker.hold ? 11.2 : 5.6, 0, 7, 5, "BulletPuff");
		A_PlaySound("weapons/autoshot", CHAN_WEAPON);
		A_GunFlash();
	}
}
