// --------------------------------------------------------------------------
//
// Plasma rifle
//
// --------------------------------------------------------------------------

class EriPlasmaRifle : DoomWeapon replaces PlasmaRifle
{
	Default
	{
		Scale 0.8;
		Weapon.SelectionOrder 100;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 40;
		Weapon.AmmoType "Cell";
		Inventory.PickupMessage "$GOTPLASMA";
		Tag "$TAG_PLASMARIFLE";
	}
	States
	{
	Ready:
		DPGG A 1 A_WeaponReady();
		Loop;
	Deselect:
		DPGG A 1 A_Lower();
		Loop;
	Select:
		DPGG A 1 A_Raise();
		Loop;
	Fire:
		DPGG A 0 A_GunFlash();
		DPGG A 0 A_FireProjectile("EriPlasmaBall",0,1,0,0);
		DPGG A 0 A_Jump(128,"Fire2");
	Fire1:
		DPGF AB 1 Bright;
		Goto FireEnd;
	Fire2:
		DPGF CD 1 Bright;
	FireEnd:
		DPGG A 1;
		DPGG B 3 A_ReFire();
		DPGG C 16; 
		DPGG B 3;
		Goto Ready;
	Flash:
		TNT1 A 1 Bright A_Light(2);
		TNT1 B 1 Bright A_Light(1);
		Goto LightDone;
	Spawn:
		DEPG A -1;
		Stop;
	}
}

class EriPlasmaBall : Actor replaces PlasmaBall
{
	Default
	{
		Radius 13;
		Height 8;
		Speed 25;
		Damage 5;
		Projectile;
		RenderStyle "Add";
		Alpha 0.75;
		SeeSound "weapons/plasmaf";
		DeathSound "weapons/plasmax";
		Obituary "$OB_MPPLASMARIFLE";
		Decal "PlasmaScorch";
		+RANDOMIZE;
	}
	States
	{
	Spawn:
		PLSS A 0;
		PLSS A 1 A_PlaySound("weapons/plasmafly", CHAN_BODY, 0.5, 1, ATTN_STATIC);
	Fly:
		PLSS ACBD 1 Bright;
		Goto Fly;
	Death:
		PLSE A 4 Bright A_StopSound(CHAN_BODY);
		PLSE BCDE 4 Bright;
		Stop;
	}
}
