// --------------------------------------------------------------------------
//
// Pistol 
//
// --------------------------------------------------------------------------

class EriPistol : EriWeapon replaces Pistol
{
	Default
	{
		Weapon.SelectionOrder 1900;
		Weapon.AmmoUse 1;
		Weapon.AmmoType "Clip";
		Weapon.SisterWeapon "AltPistol";
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED";
		Decal "BulletChip";
		Tag "$TAG_PISTOL";
		+WEAPON.WIMPY_WEAPON;
	}
	States
	{
	Ready:
		DPIG A 1 A_WeaponReady();
		Loop;
	Deselect:
		DPIG A 1 A_Lower();
		Loop;
	Select:
		DPIG A 0 A_JumpIfInventory("AltToken",1,"Trade");
		DPIG A 1 A_Raise();
		Wait;
	Trade:
		DPIG A 0 A_SelectWeapon("AltPistol");
		Goto Deselect;
	Fire:
		DPIG A 4;
		DPIF A 2 Bright X_FirePistol();
		DPIG CB 2;
		DPIG A 4;
		DPIG A 5 A_ReFire();
		Goto Ready;
	Flash:
		TNT1 A 2 Bright A_Light(2);
		TNT1 A 2 Bright A_Light(1);
		Goto LightDone;
 	Spawn:
		DEPI A -1;
		Stop;
	}

	action void X_FirePistol()
	{
		A_GunFlash();
		A_FireBullets (5.6, 0, 1, 5, "BulletPuff");
		A_PlaySound("weapons/pistol", CHAN_WEAPON);
	}
}

class AltPistol : EriPistol
{
	Default
	{
		Weapon.SisterWeapon "EriPistol";
		EriWeapon.MagSize 10;
		+WEAPON.NOAUTOFIRE;
	}
	States
	{
	Ready:
		DPIG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Select:
		DPIG A 0 A_JumpIfInventory("AltToken",1,1);
		Goto Trade;
		DPIG A 1 A_Raise();
		Wait;
	Trade:
		DPIG A 0 A_SelectWeapon("EriPistol");
		DPIG A 1 A_Lower();
		Wait;
	Fire:
		DPIF A 2 Bright X_FirePistol();
		DPIG C 2;
		DPIG B 2;
//		DPIG B 1 X_ReloadIfEmpty();
	Refire:
		DPIG BAAAAAAA 1 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD);
		goto Ready;
	DoReload:
		DPIR A 2;
		DPIR B 2 A_PlaySound("weapons/pistolclipout", CHAN_AUTO);
		DPIR C 2;
		DPIR D 6;
		DPIR EF 2;
		DPIR G 2 A_PlaySound("weapons/pistolclipin", CHAN_AUTO);
		DPIR HIJ 2;
		goto Refire;
	}
}
