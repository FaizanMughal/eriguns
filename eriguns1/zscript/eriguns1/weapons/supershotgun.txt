// --------------------------------------------------------------------------
//
// Super Shotgun
//
// --------------------------------------------------------------------------

class EriSuperShotgun : EriWeapon replaces SuperShotgun
{
	Default
	{
		Weapon.SelectionOrder 400;
		Weapon.AmmoUse 2;
		Weapon.AmmoGive 8;
		Weapon.AmmoType "Shell";
		Weapon.SisterWeapon "AltSuperShotgun";
		Inventory.PickupMessage "$GOTSHOTGUN2";
		Obituary "$OB_MPSSHOTGUN";
		Tag "$TAG_SUPERSHOTGUN";
		Decal "BulletChip";
	}
	States
	{
	Ready:
		DSSG A 1 A_WeaponReady();
		Wait;
	Deselect:
		DSSG A 1 A_Lower();
		Loop;
	Select:
		DSSG A 0 X_TradeWeapon(true);
		DSSG A 1 A_Raise();
		Wait;
	Fire:
		DSSG A 3;
		DSSF A 2 Bright A_FireShotgun2();
		DSSF B 2 Bright;
		DSSF CJ 2;
		DSSG B 2 A_CheckReload();
		DSSG CD 2;
		DSSG E 2 A_PlaySound("weapons/sshoto", CHAN_AUTO);
		DSSG FGIJK 2;
		DSSG L 2 A_PlaySound("weapons/sshotl", CHAN_WEAPON);
		DSSG MNOP 2;
		DSSG Q 2 A_PlaySound("weapons/sshotl", CHAN_WEAPON);
		DSSG RS 2;
		DSSG T 2 A_PlaySound("weapons/sshotc", CHAN_WEAPON);
		DSSG UC 2;
		DSSG A 6 A_ReFire();
		DSSG A 5 A_ReFire();
		Goto Ready;
	Flash:
		TNT1 A 1 Bright A_Light(1);
		TNT1 A 2 Bright A_Light(2);
		TNT1 A 2 Bright A_Light(1);
		Goto LightDone;
	Spawn:
		DESS A -1;
		Stop;
	}
}

class AltSuperShotgun : EriSuperShotgun
{
	Default
	{
		Weapon.AmmoGive 0;
		Weapon.AmmoUse 1;
		Weapon.AmmoType2 "Shell";
		Weapon.AmmoUse2 2;
		Weapon.SisterWeapon "EriSuperShotgun";
		EriWeapon.MagSize 2;
		+WEAPON.NOAUTOFIRE;
	}
	States
	{
	Ready:
		DSSG A 1 A_WeaponReady(WRF_ALLOWRELOAD);
		Wait;
	Select:
		DSSG A 0 X_TradeWeapon(false);
		DSSG A 1 A_Raise();
		Wait;
	Fire:
		DSSF A 0 X_FireSuperShotgunSingle();
	Fire1:
		DSSF DE 2 Bright;
		DSSF FJ 2;
		DSSG A 2;
		Goto Ready;
	Fire2:
		DSSF GH 2 Bright;
		DSSF IJ 2;
		Goto FullReload;
	AltFire:
		DSSF A 2 Bright X_FireSuperShotgunDouble();
		DSSF B 2 Bright;
		DSSF CJ 2;
		Goto FullReload;
	DoReload:
		DSSG CD 2;
		DSSG E 2 A_PlaySound("weapons/sshoto", CHAN_WEAPON);
		DSSG VWOP 2;
	EndReload:
		DSSG Q 0 X_SuperShotgunReload();
		DSSG Q 2 A_PlaySound("weapons/sshotl", CHAN_WEAPON);
		DSSG RS 2;
		DSSG T 2 A_PlaySound("weapons/sshotc", CHAN_WEAPON);
		DSSG UC 2;
		Goto Ready;
	FullReload:
		DSSG B 2 A_CheckReload();
		DSSG C 2 X_SuperShotgunReload();
		DSSG D 2;
		DSSG E 2 A_PlaySound("weapons/sshoto", CHAN_WEAPON);
		DSSG FGHIJK 2;
		DSSG L 2 A_PlaySound("weapons/sshotl", CHAN_WEAPON);
		DSSG MNOP 2;
		Goto EndReload;
	}

	action state X_FireSuperShotgunSingle()
	{
		A_GunFlash();
		A_PlaySound("weapons/sshot1bar", CHAN_AUTO);
		A_FireBullets(6.0, 3.0, 10, 5, "BulletPuff");
		return (invoker.reloadcounter == 0) ? ResolveState("Fire2") : null;
	}

	action state X_FireSuperShotgunDouble()
	{
		if(invoker.reloadcounter < 2) {
			return ResolveState("DoReload");
		}
		A_GunFlash();
		A_PlaySound("weapons/sshotf", CHAN_AUTO);
		A_FireBullets(11.2, 7.1, 20, 5, "BulletPuff");
		return null;
	}

	action void X_SuperShotgunReload()
	{
		invoker.reloadcounter = 2;
	}
}
