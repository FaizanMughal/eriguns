/*
 * eriguns1: events
 * (c) xaser 2017
 */

class EriEventHandler : StaticEventHandler
{
	override void WorldThingDied(WorldEvent e)
	{
		X_UniDrop(e.Thing);
	}

	// quasi-universal item drops. No custom actors required!
	void X_UniDrop(Actor subject)
	{
		if(subject) {
			class<Actor> subjectClass = Actor.GetReplacee(subject.GetClass());
			class<Actor> dropClass;
			
			// get the class to drop from LANGUAGE. Hooray shenanigans. :P
			// [TODO] support a comma-separated list
			string lookupString = ("UNIDROP_" .. subjectClass.GetClassName());
			string dropClassName = StringTable.Localize("$" .. lookupString);
			
			// if we got a match from LANGUAGE, continue; otherwise, idgaf.
			if(dropClassName != lookupString) {
				dropClass = dropClassName;
				if(dropClass) {
					UniDropper.X_Create(subject, dropClass);
				} else {
					subject.A_Log("[UNIDROP] Unknown actor class '" .. dropClassName .. "'");
				}
			}
		}
	}
}

// special dropper actor: this is given to a UniDrop-assigned enemy upon death,
// which waits patiently until the actor becomes non-solid; this emulates the
// behavior of regular DropItems (i.e. dropping them when A_NoBlocking is called).
// yeah, it's a good ol' inventory trick, but it's composition, yo.
class UniDropper : Inventory
{
	class<Actor> dropClass;

	Default
	{
		+INVENTORY.UNDROPPABLE;
	}
	States
	{
	Spawn:
		TNT1 A 0;
		Stop;
	}

	static void X_Create(Actor subject, class<Actor> dropClass)
	{
		if(subject && dropClass) {
			subject.A_SetInventory("UniDropper", 1);
			let dropper = UniDropper(subject.FindInventory("UniDropper"));
			if (dropper) {
				dropper.dropClass = dropClass;
			}
		}
	}

	override void DoEffect()
	{
		// if we've got a valid owner and dropclass, check if the owner is dead
		// and non-solid. If so, spawn the item and remove oneself.
		if(!self.owner || !self.dropClass) {
			X_RemoveSelf();
		} else if (self.owner.health <= 0 && !self.owner.bSolid) {
			self.owner.A_DropItem(self.dropClass);
			X_RemoveSelf();
		}
	}

	void X_RemoveSelf()
	{
		if(self.owner) {
			self.owner.A_TakeInventory(self.GetClass());
		} else {
			self.Destroy();
		}
	}
}
