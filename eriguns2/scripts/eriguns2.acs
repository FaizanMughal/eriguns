#library "eriguns2"
#include "zcommon.acs"
#import  "gamedet.acs"

int ireboltTID[64] = {0};
int ireboltDed[64] = {0};

script "GetTracerX" (void) {
	int projX = GetActorX(0);
	SetActivator(0, AAPTR_TRACER);
	int targX = GetActorX(0);
	SetResultValue(((projX - targX) * 3 / 4) >> 16);
}
script "GetTracerY" (void) {
	int projY = GetActorY(0);
	SetActivator(0, AAPTR_TRACER);
	int targY = GetActorY(0);
	SetResultValue(((projY - targY) * 3 / 4) >> 16);
}
script "GetTracerZ" (void) {
	int projZ = GetActorZ(0);
	SetActivator(0, AAPTR_TRACER);
	int targZ = GetActorZ(0);
	SetResultValue(((projZ - targZ) * 3 / 4) >> 16);
}

script "IreboltFire" (void)
{
	SetActivatorToTarget(0);
	int pNum = PlayerNumber();
	ireboltDed[pNum] = 0; // Don't do this on respawn; make the player pick up a
	                      // 'Bolt again in order to let their projectiles stick.
	SetResultValue(pNum);
}

script "IreboltHit" (int pNum)
{
	if(ireboltDed[pNum] == 0) {	
		// Get TID of bolts for this player. If none set, make one.
		int boltID = ireboltTID[pNum];
		if(boltID == 0) {
			boltID = UniqueTID(0);
			ireboltTID[pNum] = boltID;
		}	
		Thing_ChangeTID(0, boltID);
		
	} else {	
		// Just detonate DA BOMB if firing player is already dead.
		GiveInventory("IreboltDetonator", 1);
	}
}

script "IreboltDetonate" (void)
{
	int pNum = PlayerNumber();
	
	// Detonate all bolts for the calling player and reset the TID to zero.
	GiveActorInventory(ireboltTID[pNum], "IreboltDetonator", 1);
	ireboltTID[pNum] = 0;
}

script "PlayerDead" Death
{
	int pNum = PlayerNumber();
	GiveActorInventory(ireboltTID[pNum], "IreboltDetonator", 1);
	ireboltTID[pNum] = 0;
	ireboltDed[pNum] = 1;
}

script "ImmolatorReplace" (void) {
	if(CheckGame(GAME_HERETIC)) {
		TakeInventory("EriImmolator",1);
		GiveInventory("EriImmolatorAsh",1);
		SetWeapon("EriImmolatorAsh");
	} else if (CheckGame(GAME_HEXEN)|| CheckGame(GAME_STRIFE)) {
		TakeInventory("EriImmolator",1);
		GiveInventory("EriBlade",1);
		SetWeapon("EriBlade");
	}
}