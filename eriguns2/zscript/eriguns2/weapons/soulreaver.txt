/*
 * eriguns2: soul reaver
 * (c) xaser 2017
 * sprites by Amuscaria
 */

class EriSoulReaver : HellWeapon replaces BFG9000
{
	Default
	{
		Scale 0.8;
		Weapon.SelectionOrder 2800;
		Weapon.AmmoUse 10;
		Weapon.AmmoGive 10;
		Weapon.AmmoType "SoulAmmo";
		Weapon.SisterWeapon "EriSoulReaverPowered";
		Inventory.PickupMessage "$GOTSOULREAVER";
		Tag "$TAG_SOULREAVER";
		Health 3;
	}
	States
	{
	Ready:
		HSRG A 1 A_WeaponReady();
		Loop;
	Deselect:
		HSRG A 0 A_StopSound(CHAN_WEAPON);
		HSRG A 1 A_Lower();
		Wait;
	Select:
		HSRG A 1 A_Raise();
		Loop;
	Fire:
		HSRG C 3 Bright A_PlaySound("weapons/reavercharge", CHAN_BODY);
		HSRG DEF 3 Bright;
		HSRF AB 3 Bright;
		HSRF C 3 Bright A_PlaySound("weapons/reaverloop", CHAN_WEAPON, 1.0, 1);
		HSRF DEFGH 3 Bright;
		TNT1 A 0 A_Overlay(PSP_FLASH, "Overlay");
	Hold:
		TNT1 A 1;
		TNT1 A 0 A_Refire();
		TNT1 A 0 ResolveState("Release");
	Release:
		HSRF I 4 Bright X_FireSoulReaver();
		HSRF J 4 Bright;
		HSRG B 8;
		Goto Ready;
	Overlay:
		HSRF EFGH 3 Bright;
		Loop;
	Flash:
		TNT1 A 2 Bright A_Light(2);
		TNT1 A 2 Bright A_Light(1);
		Goto LightDone;
	Spawn:
		HESR A -1;
		Stop;
	}

	action void X_FireSoulReaver()
	{
		A_GunFlash("Flash");
		A_SetBlend("FF FF FF", 0.66, 15);
		A_PlaySound("weapons/reaverfire", CHAN_WEAPON);
		A_FireProjectile("SoulReaverShot", 0, 1);
	}
}

class EriSoulReaverPowered : EriSoulReaver
{
	Default
	{
		Weapon.AmmoUse 20;
		Weapon.SisterWeapon "EriSoulReaver";
		+WEAPON.POWERED_UP;
	}
	States
	{
	Release:
		HSRF I 4 Bright X_FireSoulReaverPow();
		HSRF J 4 Bright;
		HSRG B 8;
		Goto Ready;
	}

	action void X_FireSoulReaverPow()
	{
		A_GunFlash("Flash");
		A_SetBlend("FF FF FF", 1.0, 25);
		A_PlaySound("weapons/reaverpow", CHAN_WEAPON);
		A_FireProjectile("SuperSoulShot", 0, 1);
	}
}

class EriSoulReaverMana : EriSoulReaver replaces MWeapBloodscourge
{
	Default
	{
		Weapon.AmmoGive1 25;
		Weapon.AmmoGive2 25;
		Weapon.AmmoUse1 10;
		Weapon.AmmoUse2 10;
		Weapon.AmmoType1 "Mana1";
		Weapon.AmmoType2 "Mana2";
		Weapon.SisterWeapon "EriSoulReaverManaPowered";
		+WEAPON.PRIMARY_USES_BOTH;
	}
}

class EriSoulReaverManaPowered : EriSoulReaverPowered
{
	Default
	{
		Weapon.AmmoUse1 20;
		Weapon.AmmoUse2 20;
		Weapon.AmmoType1 "Mana1";
		Weapon.AmmoType2 "Mana2";
		Weapon.SisterWeapon "EriSoulReaverMana";
		+WEAPON.PRIMARY_USES_BOTH;
		+WEAPON.POWERED_UP;;
	}
}

class EriSoulReaverManaGiver : WeaponGiver
{
	Default
	{
		Inventory.PickupMessage "$GOTSOULREAVER";
		DropItem "EriSoulReaverMana";
	}
	States
	{
	Spawn:
		HESR A -1;
		Stop;
	}
}

class EriSoulReaverManaPiece : WeaponPiece
{
	Default
	{
		Inventory.PickupSound "misc/w_pkup";
		Inventory.PickupMessage "$GOTSOULREAVERPIECE";
		WeaponPiece.Weapon "EriSoulReaverMana";
	}
}

class EriSoulReaverManaPiece1 : EriSoulReaverManaPiece replaces CWeaponPiece1
{
	Default
	{
		WeaponPiece.Number 1;
	}
	States
	{
	Spawn:
		HESR B -1 Bright;
		Stop;
	}
}

class EriSoulReaverManaPiece2 : EriSoulReaverManaPiece replaces CWeaponPiece2
{
	Default
	{
		WeaponPiece.Number 2;
	}
	States
	{
	Spawn:
		HESR C -1 Bright;
		Stop;
	}
}

class EriSoulReaverManaPiece3 : EriSoulReaverManaPiece replaces CWeaponPiece3
{
	Default
	{
		WeaponPiece.Number 3;
	}
	States
	{
	Spawn:
		HESR D -1 Bright;
		Stop;
	}
}

class EriSoulReaverHexenDrop : EriSoulReaverManaGiver replaces WraithvergeDrop {}

class SoulReaverShot : Actor
{
	const SOUL_COUNT = 5;

	Default
	{
		Radius 11;
		Height 16;
		Speed 30;
		Damage 100;
		Projectile;
		Scale 0.66;
		Alpha 1.0;
		RenderStyle "Add";
		SeeSound "weapons/reaverhit";
		DeathSound "weapons/reaverhit";
		Obituary "$OB_MPSOULREAVER";
		Decal "Scorch";
		DamageType "Soul";
		+EXTREMEDEATH;
	}
	States
	{
	Spawn:
		SPIR P 1 Bright A_SpawnItemEx("SoulReaverTrail", frandom(0.5, -0.5), 0, frandom(0.5, -0.5), 0, 0, 0, 0, SXF_CLIENTSIDE);
		Loop;
	Death:
		REAX AB 3 Bright;
		REAX C 3 Bright X_SoulReaverSpirits();
		REAX DEFGHI 3 Bright;
		Stop;
	}

	void X_SoulReaverSpirits()
	{
		for(int i = 0; i < SOUL_COUNT; i++) {
			A_SpawnItemEx("SoulSpirit", xvel: 12, angle: (i * 360 / SOUL_COUNT) + frandom(-5, 5), flags: SXF_TRANSFERPOINTERS);
		}
	}
}

class SoulReaverTrail : EriTrail
{
	Default
	{
		Scale 0.15;
		Alpha 1.0;
		Renderstyle "Add";
	}
	States
	{
	Spawn:
		REAX KKKJ 1 Bright;
		REAX JJ 1 Bright A_FadeOut(0.25);
		Loop;
	}
}

class SoulSpirit : SoulReaverShot
{
	int seekTics;
	property SeekTics:seekTics;

	Default
	{
		Radius 8;
		Height 4;
		Speed 12;
		Damage 2;
		BounceType "Hexen";
		WallBounceFactor 1.0;
		BounceFactor 1.0;
		Scale 1.0;
		SeeSound "weapons/reaversoulout";
		Decal "PlasmaScorchLower";
		DamageType "SoulRipper";
		SoulSpirit.SeekTics 105;
		+RIPPER;
		+SEEKERMISSILE;
	}
	States
	{
	Spawn:
		REAS A 1 Bright NoDelay X_SoulSeek();
		Loop;
	Debounce:
		REAS A 0 Bright X_SoulDebounce();
		REAS A 1 Bright A_SpawnItemEx("SoulReaverTrail", frandom(0.5, -0.5), 0, frandom(0.5, -0.5), 0, 0, 0, 0, SXF_CLIENTSIDE);
		Wait;
	Death:
		REAX A 3 Bright A_SetScale(0.5);
		REAX B 3 Bright;
		REAX C 3 Bright;
		REAX DEFGHI 3 Bright;
		Stop;
	}

	state X_SoulSeek()
	{
		if(self.seekTics <= 1) {
			return ResolveState("Debounce");
		}
		self.seekTics--;
		A_SeekerMissile(17, 17, SMF_LOOK | SMF_PRECISE);
		A_SpawnItemEx("SoulReaverTrail", frandom(0.5, -0.5), 0, frandom(0.5, -0.5), 0, 0, 0, 0, SXF_CLIENTSIDE);
		return null;
	}

	void X_SoulDebounce()
	{
		self.bBounceOnWalls    = false;
		self.bBounceOnCeilings = false;
		self.bBounceOnFloors   = false;
	}
}

class SuperSoulShot : SoulReaverShot
{
	Default
	{
		Speed 10;
		Damage 5;
		Obituary "$OB_MPSOULREAVERPOW";
		DamageType "SoulRipper";
		Scale 1.0;
		+RIPPER;
	}
	States
	{
	Spawn:
		REAB A 0 Bright NoDelay A_PlaySound("weapons/reaverloop2", CHAN_BODY, 1.0, 1);
	Fly:
		REAB ABCDE 3 Bright A_SpawnItemEx("SuperSoulSpirit", 0, 0, 0, 12, 0, 0, random(1, 360), SXF_TRANSFERPOINTERS, 0);
		Loop;
	Death:
		REAB F 3 Bright;
		REAX BCDEFGHI 3 Bright;
		Stop;
	}
}

class SuperSoulSpirit : SoulSpirit
{
	Default
	{
		Damage 1;
		Obituary "$OB_MPSOULREAVERPOW";
		SeeSound "weapons/reaversoulfly";
		BounceCount 2;
	}
}
