/*
 * eriguns2: templar baller knight sword
 * (c) xaser 2017
 * sprites (c) Amuscaria
 */

class EriSword : HellWeapon replaces Fist
{
	Default
	{
		Weapon.SelectionOrder 3700;
		Weapon.Kickback 100;
		Obituary "$OB_MPSWORD";
		Tag "$TAG_SWORD";
		+WEAPON.WIMPY_WEAPON;
		+WEAPON.MELEEWEAPON;
		+WEAPON.NOALERT;
	}
	States
	{
	Ready:
		HTSG A 1 A_WeaponReady();
		Loop;
	Deselect:
		HTSG A 1 A_Lower();
		Loop;
	Select:
		HTSG A 1 A_Raise();
		Loop;
	Fire:
		TNT1 A 2;
		HTSG B 2 X_SwordSwing  (20,  0);
		HTSG B 2 A_WeaponOffset( 0, 48);
		HTSG C 2 X_SwordHit    ( 0, 32);
		HTSG D 2;
		HTSG E 2;
		HTSG E 2 A_WeaponOffset(-16, 80);
		TNT1 A 3 A_WeaponOffset(  0, 32);
		TNT1 A 5 A_ReFire();
	RaiseUp:
		HTSG A 1 A_WeaponOffset(10, 72);
		HTSG A 1 A_WeaponOffset( 8, 64);
		HTSG A 1 A_WeaponOffset( 6, 56);
		HTSG A 1 A_WeaponOffset( 4, 48);
		HTSG A 1 A_WeaponOffset( 2, 40);
		Goto Ready;
	Spawn:
		HETZ A -1;
		Stop;
	}

	action void X_SwordSwing(double offsetX, double offsetY)
	{
		A_WeaponOffset(offsetX, offsetY);
		A_PlaySound("weapons/swordswing", CHAN_AUTO);
	}

	action void X_SwordHit(double offsetX, double offsetY)
	{
		A_WeaponOffset(offsetX, offsetY);
		int baseDamage = (FindInventory("PowerStrength") || FindInventory("PowerWeaponLevel2")) ? 20 : 4;
		A_CustomPunch(baseDamage * random(1, 10), 1, 0, "SwordPuff", 96);
	}
}

class SwordPuff : BulletPuff
{
	Default
	{
		+PUFFGETSOWNER;
		+PUFFONACTORS;
		AttackSound "weapons/swordclunk";
		SeeSound "weapons/swordsplat";
	}
	States
	{
	Spawn:
	Melee:
		Goto Super::Melee;
	XDeath:
		TNT1 A 0;
		TNT1 A 1 A_AlertMonsters();
		Stop;
	}
}
