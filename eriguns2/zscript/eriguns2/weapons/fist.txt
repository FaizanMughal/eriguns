// --------------------------------------------------------------------------
//
// Fist 
//
// --------------------------------------------------------------------------

class EriFist : HellWeapon replaces Fist
{
	Default
	{
		Weapon.SelectionOrder 3700;
		Weapon.Kickback 100;
		Obituary "$OB_MPFIST";
		Tag "$TAG_FIST";
		+WEAPON.WIMPY_WEAPON;
		+WEAPON.MELEEWEAPON;
		+WEAPON.NOALERT;
	}
	States
	{
	Ready:
		PUNG A 1 A_WeaponReady();
		Loop;
	Deselect:
		PUNG A 1 A_Lower();
		Loop;
	Select:
		PUNG A 1 A_Raise();
		Loop;
	Fire:
		PUNG B 4;
		PUNG C 4 X_Punch();
		PUNG D 5;
		PUNG C 4;
		PUNG B 5 A_ReFire();
		Goto Ready;
	}

	action void X_Punch()
	{
		int baseDamage = FindInventory("PowerStrength") ? 20 : 2;
		A_CustomPunch(baseDamage * random(1, 10), 1, 0, "FistPuff", meleesound: "weapons/fist");
	}
}

class FistPuff : BulletPuff
{
	Default
	{
		+PUFFGETSOWNER;
		+PUFFONACTORS;
		SeeSound "*fist";
	}
	States
	{
	Spawn:
	Melee:
		Goto Super::Melee;
	XDeath:
		TNT1 A 0;
		TNT1 A 1 A_AlertMonsters();
		Stop;
	}
}
