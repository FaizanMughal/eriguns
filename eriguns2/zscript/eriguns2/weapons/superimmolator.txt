/*
 * eriguns2: super immolator
 * (c) xaser 2017
 * sprites by Amuscaria
 */

class EriSuperImmolator : HellWeapon
{
	bool soundToggle;

	Default
	{
		Scale 0.8;
		Weapon.SelectionOrder 1900;
		Weapon.AmmoUse 2;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "FlameAmmo";
		Weapon.SisterWeapon "EriSuperImmolatorPowered";
		Inventory.Pickupmessage "$GOTSUPERLATOR";
		Tag "$TAG_SUPERLATOR";
		+WEAPON.WIMPY_WEAPON;
	}
	States
	{
	Ready:
		HSIG A 1 A_WeaponReady();
		Loop;
	Deselect:
		HSIG A 0 X_SuperImmolatorEnd();
		HSIG A 1 A_Lower();
		Wait;
	Select:
		HSIG A 1 A_Raise();
		Wait;
	Fire:
		HSIG A 0 A_PlaySound("weapons/immolatorstart", CHAN_AUTO);
		HSIG B 2 A_WeaponOffset(0, 36);
		HSIF C 2 Bright A_WeaponOffset(0, 40);
	Hold:
		HSIF A 1 Bright A_GunFlash();
		HSIF B 2 Bright X_FireSuperImmolator();
		HSIF C 2 Bright;
		HSIF E 0 A_Refire();
		HSIF E 2 X_SuperImmolatorEnd();
		HSIF F 2 A_WeaponOffset(0, 36);
		HSIF G 2 A_WeaponOffset(0, 32);
		HSIF HI 2;
		Goto Ready;
	Flash:
		TNT1 A 1 A_Light(1);
		TNT1 A 2 A_Light(2);
		TNT1 A 1 A_Light(1);
		Goto LightDone;
 	Spawn:
		HESI A -1;
		Stop;
	}

	action void X_FireSuperImmolator()
	{
		A_StopSound(CHAN_WEAPON);
		A_FireProjectile("SuperImmolatorFireball");
		A_PlaySound("weapons/immolatorsuper", invoker.soundToggle ? CHAN_5 : CHAN_6);
		invoker.soundToggle = !invoker.soundToggle;
	}

	action void X_SuperImmolatorEnd()
	{
		A_StopSound(invoker.soundToggle ? CHAN_6 : CHAN_5);
		invoker.soundToggle = false;
	}
}

class EriSuperImmolatorPowered : EriSuperImmolator
{
	Default
	{
		Weapon.SisterWeapon "EriSuperImmolator";
		+WEAPON.POWERED_UP;
	}
	States
	{
	Deselect:
		HSIG A 0 X_SuperImmolatorPowEnd();
		HSIG A 1 A_Lower();
		Wait;
	Fire:
		HSIG A 0 A_PlaySound("weapons/immolatorstart", CHAN_AUTO);
		HSIG B 2 A_WeaponOffset(0, 36);
		HSIF C 2 Bright A_WeaponOffset(0, 40);
		HSIF C 0 X_SuperImmolatorPowStart();
	Hold:
		HSIF B 2 Bright X_FireSuperImmolatorPow(true);
		HSIF D 2 Bright X_FireSuperImmolatorPow();
		HSIF C 2 Bright X_SuperImmolatorPowRefire();
		HSIF E 2 X_SuperImmolatorPowEnd();
		HSIF F 2 A_WeaponOffset(0, 36);
		HSIF G 2 A_WeaponOffset(0, 32);
		HSIF HI 2;
		Goto Ready;
	Flash:
		TNT1 A 1 A_Light(2);
		TNT1 A 1 A_Light(1);
		Goto LightDone;
	}

	action void X_SuperImmolatorPowStart()
	{
		A_PlaySound("weapons/immolatorburn2", CHAN_WEAPON, 1.0, true);
	}

	action void X_FireSuperImmolatorPow(bool flash = false)
	{
		A_PlaySound("weapons/immolatorsuper", invoker.soundToggle ? CHAN_5 : CHAN_6);
		A_FireProjectile("SuperImmolatorFirestream", frandom(-4, 4), true);
		A_WeaponOffset(frandom(-2, 2), frandom(38, 42));
		if(flash) {
			A_GunFlash();
		}
	}

	action void X_SuperImmolatorPowRefire()
	{
		A_Refire();
		A_WeaponOffset(0, 40);
	}

	action void X_SuperImmolatorPowEnd()
	{
		A_StopSound(CHAN_WEAPON);
		A_StopSound(invoker.soundToggle ? CHAN_6 : CHAN_5);
		invoker.soundToggle = false;
	}
}

class EriSuperImmolatorAsh : EriSuperImmolator
{
	Default
	{
		Weapon.AmmoUse 1;
		Weapon.AmmoType "AshAmmo";
		Weapon.SisterWeapon "EriSuperImmolatorAshPowered";
	}
}

class EriSuperImmolatorAshPowered : EriSuperImmolatorPowered
{
	Default
	{
		Weapon.AmmoUse 1;
		Weapon.AmmoType "AshAmmo";
		Weapon.SisterWeapon "EriSuperImmolatorAsh";
		+WEAPON.POWERED_UP;
	}
}

class EriSuperImmolatorMana : EriSuperImmolator replaces FighterWeaponPiece
{
	Default
	{
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 25;
		Weapon.AmmoType "Mana1";
		Weapon.SisterWeapon "EriSuperImmolatorManaPowered";
	}
}

class EriSuperImmolatorManaPowered : EriSuperImmolatorPowered
{
	Default
	{
		Weapon.AmmoUse 1;
		Weapon.AmmoType "Mana1";
		Weapon.SisterWeapon "EriSuperImmolatorMana";
		+WEAPON.POWERED_UP;
	}
}

class EriSuperImmolatorManaGiver : WeaponGiver
{
	Default
	{
		Inventory.PickupMessage "$GOTSUPERLATOR";
		DropItem "EriSuperImmolatorMana";
	}
	States
	{
	Spawn:
		HESI A -1;
		Stop;
	}
}

class EriSuperImmolatorHexen0 : EriSuperImmolatorManaGiver replaces QuietusDrop   {}
class EriSuperImmolatorHexen1 : EriSuperImmolatorManaGiver replaces FWeaponPiece1 {}
class EriSuperImmolatorHexen2 : EriSuperImmolatorManaGiver replaces FWeaponPiece2 {}
class EriSuperImmolatorHexen3 : EriSuperImmolatorManaGiver replaces FWeaponPiece3 {}
class EriSuperImmolatorHexen4 : EriSuperImmolatorManaGiver replaces FWeapQuietus  {}

class SuperImmolatorFireball : ImmolatorFireball
{
	Default
	{
		Speed 25;
		Obituary "$OB_MPSUPERLATOR";
	}
}

class SuperImmolatorFirestream : ImmolatorFirestream
{
	Default
	{
		Obituary "$OB_MPSUPERLATORPOW";
		Scale 0.6;
		Speed 40;
	}
}
