/*
 * eriguns2: super immolator
 * (c) xaser 2017
 * sprites by Amuscaria
 */

class EriSuperImmolator : HellWeapon
{
	bool soundToggle;

	Default
	{
		Scale 0.8;
		Weapon.SelectionOrder 1900;
		Weapon.AmmoUse 2;
		Weapon.AmmoGive 0;
		Weapon.AmmoType "FlameAmmo";
		Weapon.SisterWeapon "EriSuperImmolatorPowered";
		Inventory.Pickupmessage "$GOTSUPERLATOR";
		Tag "$TAG_SUPERLATOR";
		+WEAPON.WIMPY_WEAPON;
	}
	States
	{
	Ready:
		HSIG A 1 A_WeaponReady();
		Loop;
	Deselect:
		HSIG A 1 A_Lower();
		Loop;
	Select:
		HSIG A 1 A_Raise();
		Wait;
	Fire:
		HSIG A 0 A_PlaySound("weapons/immolatorstart", CHAN_WEAPON);
		HSIG B 2 A_WeaponOffset(0, 36);
		HSIF C 2 Bright A_WeaponOffset(0, 40);
	Hold:
		HSIF A 1 Bright A_GunFlash();
		HSIF B 2 Bright X_FireSuperImmolator();
		HSIF C 2 Bright;
		HSIF E 0 A_Refire();
		HSIF E 2 X_SuperImmolatorEnd();
		HSIF F 2 A_WeaponOffset(0, 36);
		HSIF G 2 A_WeaponOffset(0, 32);
		HSIF HI 2;
		Goto Ready;
	Flash:
		TNT1 A 1 A_Light(1);
		TNT1 A 2 A_Light(2);
		TNT1 A 1 A_Light(1);
		Goto LightDone;
 	Spawn:
		HESI A -1;
		Stop;
	}

	action void X_FireSuperImmolator()
	{
		A_FireProjectile("SuperImmolatorFireball");
		A_PlaySound("weapons/immolatorsuper", invoker.soundToggle ? CHAN_5 : CHAN_6);
		invoker.soundToggle = !invoker.soundToggle;
	}

	action void X_SuperImmolatorEnd()
	{
		A_StopSound(invoker.soundToggle ? CHAN_6 : CHAN_5);
		invoker.soundToggle = false;
	}
}

class EriSuperImmolatorPowered : EriSuperImmolator
{
	Default
	{
		Weapon.SisterWeapon "EriSuperImmolator";
		+WEAPON.POWERED_UP;
	}
}

class SuperImmolatorFireball : ImmolatorFireball
{
	Default
	{
		Speed 25;
		Obituary "$OB_MPSUPERLATOR";
	}
}
