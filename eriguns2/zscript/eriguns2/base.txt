/*
 * eriguns2: base actor defs
 * (c) xaser 2017
 */

class EriTrail : Actor
{
	Default
	{
		-RANDOMIZE;
		+NOBLOCKMONST;
		+NOTELEPORT;
		+FORCEXYBILLBOARD;
		+NOBLOCKMAP;
		+NOGRAVITY;
		+THRUACTORS;
	}
}

class EriDrop : EriTrail
{
	Default
	{
		Projectile;
		Damage 0;
		Speed 0;
		-NOGRAVITY;
	}
}

class EriPuff : Actor
{
	Default
	{
		+NOGRAVITY;
		+PUFFGETSOWNER;
		+PUFFONACTORS;
	}
}

class EriToken : Inventory
{
	Default
	{
		+INVENTORY.UNDROPPABLE;
	}
}

class EriStickyMissile : Actor
{
	Vector3 stickPosition;
	float stickFactor;
	float minZ;
	
	Property MinZ:minZ;
	Property StickFactor:stickFactor;

	Default
	{
		Projectile;
		EriStickyMissile.StickFactor 0.75;
	}

	override int SpecialMissileHit(Actor victim)
	{
		if(!self.tracer && victim && victim != self.target) {
			// stick the projectile; this just sets the tracer
			// and position; it's up to the calling actor to call
			// X_StickyWarp whenever warping is desired, for full control.
			self.tracer = victim;

			// store the relative offset for use in X_StickyWarp later. We'll
			// by default treat the actor as 75% its actual size in x/y space so
			// the missile will physically "stick" inside the actor they hit.
			self.stickPosition = self.tracer.Vec3To(self);
			self.stickPosition.x = self.stickPosition.x * self.stickFactor;
			self.stickPosition.y = self.stickPosition.y * self.stickFactor;
		}
		return -1;
	}

	void X_StickyWarp()
	{
		// actually make the projectile stick to the tracer.
		A_Warp(AAPTR_TRACER, stickPosition.x, stickPosition.y, max(stickPosition.z, self.minZ), 0, WARPF_ABSOLUTEOFFSET|WARPF_USECALLERANGLE|WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE);
	}
}
