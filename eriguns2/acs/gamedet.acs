#library "gamedet"
#include "zcommon.acs"

// Simple game detection lib.
str GAME_DOOM    = "GameDetectorDoom";
str GAME_HERETIC = "GameDetectorHeretic";
str GAME_HEXEN   = "GameDetectorHexen";
str GAME_STRIFE  = "GameDetectorStrife";
str GAME_CHEX    = "GameDetectorChex";

// Method #1: Place a token into the player's inventory depending on game.
script "DetectGame" ENTER {
	int testTID = UniqueTID();
	SpawnForced("MapSpot",GetActorX(0),GetActorY(0),GetActorZ(0),testTID,0);
	Thing_SpawnNofog(testTID,255,0,testTID);
	     if(ThingCountName(GAME_DOOM   ,0) > 0) GiveInventory("GameDoom"   , 1);
	else if(ThingCountName(GAME_HERETIC,0) > 0) GiveInventory("GameHeretic", 1);
	else if(ThingCountName(GAME_HEXEN  ,0) > 0) GiveInventory("GameHexen"  , 1);
	else if(ThingCountName(GAME_STRIFE ,0) > 0) GiveInventory("GameStrife" , 1);
	else if(ThingCountName(GAME_CHEX   ,0) > 0) GiveInventory("GameChex"   , 1);
	Thing_Remove(testTID);
}

// Method #2: Static game-checker function. Useful for ENTER scripts that need
// to determine game on the first tic (since the above won't work on tic 1).
function bool CheckGame (str game) {
	
	bool isGame = false;
	int testTID = UniqueTID();
	SpawnForced("MapSpot",GetActorX(0),GetActorY(0),GetActorZ(0),testTID,0);
	Thing_SpawnNofog(testTID,255,0,testTID);	
	if(ThingCountName(game,0) > 0) {
		isGame = true;
	}	
	Thing_Remove(testTID);
	return isGame;
}